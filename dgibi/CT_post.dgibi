**********************
* CT post-traitement *
**********************

OPTI ECHO 0 ;      
OPTI TRAC 'PSC' ;                  
OPTI 'FTRA' 'CT_post.ps' ;
OPTI 'ERRE' 'FATAL' ;

'OPTI' 'ACQU' 'data_calcul.dgibi';

'MESS' '*****************************';

* 2D ou 3D:
'ACQU' MM0*'MOT';
'ACQU' val0*'ENTIER';
DIME0 = val0;
'MESS' ' 2D-3D  = ' (DIME0) ;

* Elements LINE0s ou quadratiques:
'ACQU' MM0*'MOT';
'ACQU' val0*'ENTIER';
LINE0 = val0;
'MESS' ' Linear elements  = ' (LINE0) ;

* Elements a integration selective:
'ACQU' MM0*'MOT';
'ACQU' val0*'ENTIER';
BBAR0 = val0;
'MESS' ' Selective integration  = ' (BBAR0) ;

* Elements a integration REDU0e:
'ACQU' MM0*'MOT';
'ACQU' val0*'ENTIER';
REDU0 = val0;
'MESS' ' Reduced integration  = ' (REDU0) ;

* Damage :
'ACQU' MM0*'MOT';
'ACQU' val0*'ENTIER';
GTN = val0;
'MESS' ' Damage  = ' (GTN) ;

* Thermal fluence
'ACQU' MM0*'MOT';
'ACQU' val0*'FLOTTANT';
PHI  = val0;
'MESS' ' Thermal fluence  = ' (PHI) ;

OPTI COSC BLANC ;
OPTI REST 'CT_calc1.sauv' ;
REST ;

*-------------------------------------------------------------------*
*                   PARAMETRES DU POST TRAITEMENT                   *
*-------------------------------------------------------------------*

*NBNE est le nombre de points de Gauss par élément {4|8}
SI (DIME0 EGA 2) ;
  NBNE    = 4 ;
  SINON ;
    NBNE  = 8 ;
FINSI ;

*OU SOUHAITEZ-VOUS REGARDER LES POINTS DE GAUSS
BAS   = VRAI ;

*LOI POINTS PAR POINTS
PP    = FAUX ;

*NUMERO DE L ELEMENT POUR LES CHAMPS AUX POINTS DE GAUSS
NE    = 1 ;

*ORIENTATION DU LIGAMENT SELON X (CT12.5) 
*DU CODE LORS DU CHANGEMENT
ORI_LIEN = 'X' ;

*CREATION DES TABLES POUR RECUPERER LES INFORMATIONS SUR LE 
*LIGAMENT
TABLE_SORTIE            = TABLE ;
TABLE_SORTIE.'SIG_H'    = TABLE ;
TABLE_SORTIE.'SIG_1'    = TABLE ;
TABLE_SORTIE.'DEF_PLAS' = TABLE ;
TABLE_SORTIE.'TRIAX'    = TABLE ;
TABLE_SORTIE.'PORO'     = TABLE ;
TABLE_SORTIE.'SIG_YY'   = TABLE ;
TABLE_SORTIE.'SIG_ZZ'   = TABLE ;
TABLE_SORTIE.'COOR_X'   = TABLE ;
TABLE_SORTIE.'COOR_Z'   = TABLE ;

*CREATION DES TABLES POUR RECUPERER LES INFORMATIONS AUX
*POINTS DE GAUSS
TABLE_SORTIE.'SIG_H_PG'    = TABLE ;
TABLE_SORTIE.'SIG_1_PG'    = TABLE ;
TABLE_SORTIE.'DEF_PLAS_PG' = TABLE ;
TABLE_SORTIE.'TRIAX_PG'    = TABLE ;
TABLE_SORTIE.'PORO_PG'     = TABLE ;
TABLE_SORTIE.'SIG_YY_PG'   = TABLE ;
TABLE_SORTIE.'SIG_ZZ_PG'   = TABLE ;

*CREATION DES LISTES POUR LES CHAMPS DES CONTRAINTES AUX POINTS DE GAUSS
SIG_YY_gs_1_A = PROG ; SIG_YY_gs_2_A = PROG ;
SIG_YY_gs_3_A = PROG ; SIG_YY_gs_4_A = PROG ;

DEF_PLAS_gs_1_A = PROG ; DEF_PLAS_gs_2_A = PROG ;
DEF_PLAS_gs_3_A = PROG ; DEF_PLAS_gs_4_A = PROG ;

TRIAX_gs_1_A = PROG ; TRIAX_gs_2_A = PROG ;
TRIAX_gs_3_A = PROG ; TRIAX_gs_4_A = PROG ;

PORO_gs_1_A = PROG ; PORO_gs_2_A = PROG ;
PORO_gs_3_A = PROG ; PORO_gs_4_A = PROG ;

SIG_H_gs_1_A = PROG ; SIG_H_gs_2_A = PROG ;
SIG_H_gs_3_A = PROG ; SIG_H_gs_4_A = PROG ;

SIG_1_gs_1_A = PROG ; SIG_1_gs_2_A = PROG ;
SIG_1_gs_3_A = PROG ; SIG_1_gs_4_A = PROG ;


*UNIFORMISATION DES NOMS
SI  (GTN EGA 1) ;
  MAILO       = TAB1.'MAI_ACT' ;       MODO = TAB1.'MO_ACT' ; 
  CONTRAINTE0 = TAB1.'MACONTRAINTES' ; VAR_INTERNE0 = TAB1.'MESVAR_INTERNES' ;
  REACTION0   = TAB1.'MAREACTIONS' ;   DEPLACEMENT0 = TAB1.'MONDEPLACEMENTS' ;
  SINON ;
    MAILO       = TAB1.'MAI_ACT' ;     MODO = TAB1.'MO_ACT' ; 
    CONTRAINTE0 = TAB1.'CONTRAINTES' ; VAR_INTERNE0 = TAB1.'VARIABLES_INTERNES' ;
    REACTION0   = TAB1.'REACTIONS' ;   DEPLACEMENT0 = TAB1.'DEPLACEMENTS' ;
FINSI ;    
  
*LISTE POUR LA RECUPERATION DE LA FORCE ET DU DEPLACEMENT
LFORC = PROG ;
LFORC = INSE LFORC 1 (0.) ;

LDEPL = PROG ;
LDEPL = INSE LDEPL 1 (0.) ;
 
NBPAS  = (DIME (CONTRAINTE0))- 1 ;
TAILLE = ENTI (NBPAS) ;

* FIXER L ECHELLE DES ANIMATIONS

PLASMIN0 = 0 ; PLASMAX0 = 0 ; SIGYMIN0 = 0 ;
SIGYMAX0 = 0 ; VONMMIN0 = 0 ; VONMMAX0 = 0 ;

SI (GTN EGA 1) ;
  POROMIN0 = 0 ; BROKMIN0 = 0 ;
  POROMAX0 = 0 ; BROKMAX0 = 0 ;
   
FINSI ;

REPE I (TAILLE) ;
    
  SIG_YY0   = REDU (EXCO (CONTRAINTE0. &I) 
  'SMYY') (MAILO.&I) ;

  SIGYMIN1 = MINI SIG_YY0 ;
  SIGYMAX1 = MAXI SIG_YY0 ;
    

  SIG_VM0  = VMIS (MODO. &I) 
  (CONTRAINTE0. &I) ;

  VONMMIN1 = MINI SIG_VM0 ;
  VONMMAX1 = MAXI SIG_VM0 ;
    

  EPS_PLAS0 = REDU (EXCO (VAR_INTERNE0. &I) 'P') 
  (MAILO.&I) ;
  SI (PP EGA VRAI) ;
  EPS_PLAS0 = REDU (EXCO (VAR_INTERNE0. &I) 'EPSE') 
  (MAILO.&I) ;
  FINSI ;        

  PLASMIN1 = MINI EPS_PLAS0 ;
  PLASMAX1 = MAXI EPS_PLAS0 ;

  SI (GTN EGA 1) ;
    PORO_END0 = REDU (EXCO (VAR_INTERNE0. &I) 'F') 
               (MAILO.&I) ;
    POROMIN1 = MINI PORO_END0 ;
    POROMAX1 = MAXI PORO_END0 ;
               
    
    BROK_END0 = REDU (EXCO (VAR_INTERNE0. &I) 'BROK') 
           (MAILO.&I) ;
    BROKMIN1 = MINI BROK_END0 ;
    BROKMAX1 = MAXI BROK_END0 ; 
  FINSI ;
    
  SI ( SIGYMIN1 < SIGYMIN0) ;
    SIGYMIN0 = SIGYMIN1 ;
  FINSI ;
  SI ( SIGYMAX1 > SIGYMAX0) ;
    SIGYMAX0 = SIGYMAX1 ;
  FINSI ;

  SI ( VONMMIN1 < VONMMIN0) ;
    VONMMIN0 = VONMMIN1 ;
  FINSI ;
  SI ( VONMMAX1 > VONMMAX0) ;
    VONMMAX0 = VONMMAX1 ;
  FINSI ;
  
  SI ( PLASMIN1 < PLASMIN0) ;
    PLASMIN0 = PLASMIN1 ;
  FINSI ;
  SI ( PLASMAX1 > PLASMAX0) ;
    PLASMAX0 = PLASMAX1 ;
  FINSI ;

  SI (GTN EGA 1) ;
    SI ( POROMIN1 < POROMIN0) ;
      POROMIN0 = POROMIN1 ;
    FINSI ;
    SI ( POROMAX1 > POROMAX0) ;
      POROMAX0 = POROMAX1 ;
    FINSI ;
    
    SI ( BROKMIN1 < BROKMIN0) ;
    BROKMIN0 = BROKMIN1 ;
    FINSI ;
    SI ( BROKMAX1 > BROKMAX0) ;
      BROKMAX0 = BROKMAX1 ;
    FINSI ;
  FINSI ;
    
FIN I ;

ECHPLAS = PROG PLASMIN0 PAS ((PLASMAX0 - PLASMIN0)/20) PLASMAX0 ;
ECHSIGY = PROG SIGYMIN0 PAS ((SIGYMAX0 - SIGYMIN0)/20) SIGYMAX0 ;
ECHVONM = PROG VONMMIN0 PAS ((VONMMAX0 - VONMMIN0)/20) VONMMAX0 ;
SI (GTN EGA 1) ;
  ECHPORO = PROG POROMIN0 PAS ((POROMAX0 - POROMIN0)/20) POROMAX0 ;
  ECHBROK = PROG BROKMIN0 PAS ((BROKMAX0 - BROKMIN0)/20) BROKMAX0 ;
FINSI ;

REPE I (TAILLE) ;

*SELECTION DES ELEMENTS DONT AU MOINS UN NOEUD EST SUR LE LIGAMENT
  SI (DIME0 EGA 2);
    P_LIGA      = LIGNELIGA ;
	  SINON ;
    CHX CHY CHZ = COOR Lg0 ;
    P_LIGA      = POINT CHX 'INFERIEUR' (a_2) ;
	FINSI ;

*TABLE POUR LA VISUALISATION DES CHAMPS AU POINTS DE GAUSS
  TABV      = VISUPG (MODO.&I) ;
*NOMBRE DE NOEUDS SUR LE LIGAMENT
  NB        = NBNO P_LIGA ;
  ELT_LIGA  = (MAILO.&I) ELEM 'APPUYE' 
             'LARGEMENT' P_LIGA ;
*COORD X DES PTS DE GAUSS
  AX1       = CHAN 'CHAM' (COOR 1 (MAILO.&I)) 
              (MODO.&I) 'STRESSES' ;
  AX        = REDU AX1 ELT_LIGA ;
*COORD Y DES PTS DE GAUSS
  AY1       = CHAN 'CHAM' (COOR 2 (MAILO.&I)) 
              (MODO.&I) 'STRESSES' ;
  AY        = REDU AY1 ELT_LIGA ;
	SI (DIME0 EGA 3) ;
*COORD Z DES PTS DE GAUSS
    AZ1     = CHAN 'CHAM' (COOR 3 (MAILO.&I)) 
              (MODO.&I) 'STRESSES' ;
    AZ      = REDU AZ1 ELT_LIGA ;             		  
	FINSI ;
		  
*FORCE
  LREAC_I   = REDU (REACTION0.&I) CHARn ;
  ER_I      = MAXI(EXCO 'FY' (RESU LREAC_I)) ;
  SI (DIME0 EGA 3) ;  
    LFORC   = LFORC ET ((PROG ER_I)*2.) ; 
    SINON ;
      LFORC = LFORC ET ((PROG ER_I)*12.) ;
  FINSI ;
*DEPLACEMENT
  LDP_I     = REDU (DEPLACEMENT0.&I) CMOD ;
  EDP_I     = MAXI (EXCO 'UY' (RESU LDP_I)) ;
  SI (DIME0 EGA 3) ;
    LDEPL   = LDEPL ET ((PROG EDP_I)*2.) ;
    SINON ;
      LDEPL = LDEPL ET ((PROG EDP_I)*2.) ;
  FINSI ;
* DEFORMEE
  DEFOT_I   = DEPLACEMENT0.&I ;
  DEFO_I    = DEFO DEFOT_I (MAILO.&I) 1. ; 
    
*EXTRACTION DES CHAMPS*
*---------------------*

*EXTRACTION DE SIGMA YY SOIT SIGMA OUVERTURE
  SIG_YY    = REDU (EXCO (CONTRAINTE0. &I)
  'SMYY') ELT_LIGA ;
*EXTRACTION DE LA VAR INTERNE D ECROUISSAGE
  EPS_PLAS  = REDU (EXCO (VAR_INTERNE0. &I) 
  'P') ELT_LIGA ;
  SI (PP EGA VRAI) ;
    EPS_PLAS  = REDU (EXCO (VAR_INTERNE0. &I) 
   'EPSE') ELT_LIGA ;
  FINSI ;
  SI (GTN EGA 1) ;  
*EXTRACTION DE LA VAR INTERNE D ENDOMMAGEMENT
    PORO_END  = REDU (EXCO (VAR_INTERNE0. &I) 
    'F') ELT_LIGA ;
  FINSI ;
*CALCUL DU CHAMPS DE CONTRAINTE HYDROSTATIQUE
  I1 I2 I3  = INVA (MODO. &I) 
  (CONTRAINTE0. &I) ;
  SIG_H     = (REDU I1 ELT_LIGA)/3. ;
*CALCUL DE LA PREMIERE CONTRAINTE PRINCIPALE
  TENS_SIG  = PRIN (CONTRAINTE0. &I) 
  (MODO. &I) ;
  SIG_1     = REDU (EXCO TENS_SIG 'SI11') ELT_LIGA ;
*CALCUL DU CHAMPS DE TAUX DE TRIAXIALITE
  SIG_VM      = VMIS (MODO. &I) 
  (CONTRAINTE0. &I) ;
  SCAL_SIG_VM = CHAN TYPE SIG_VM SCALAIRE ;
  SCAL_SIG_VM = REDU SCAL_SIG_VM ELT_LIGA ;
  TAUX_TRIAX  = SIG_H/SCAL_SIG_VM ;	
    
  
*LISTES DE SAUVEGARDES
  LAX   = PROG ; LAX0  = PROG ; LAY = PROG ;
	LAX_i = PROG ; LAY_i = PROG ;
	
  SI (DIME0 EGA 3) ;
	  LAZ   = PROG ;
	  LAZ_i = PROG ;
  FINSI ;
	
  LSIG_YY   = PROG ; LSIG_H = PROG ; LSIG_1 = PROG ;
  LEPS_PLAS = PROG ; LTRIAX = PROG ;
  
	SI (GTN EGA 1) ;
    LPORO = PROG ;
	FINSI ;
  
*** BOUCLE PERMETTANT D ASSOCIER LA BONNE VALEUR 
*** DES CHAMPS AUX PONITS DE GAUSS
  REPE N_ELT (NBEL ELT_LIGA) ; COMM 'BOUCLE SUR LES ELEMENTS' ;
    MESS 'ELT' &N_ELT ;
*LISTE INTERMEDIAIRE POUR LE STOCK DE TOUS LES PI DE L ELEMENTS
    L_i_VALAX = PROG ; 
    L_i_VALAY = PROG ;
	  SI (DIME0 EGA 3) ;
      L_i_VALAZ = PROG ;
	  FINSI ;
    
    L_i_VAL_SIG_YY   = PROG ; L_i_VAL_SIG_H    = PROG ;
    L_i_VAL_SIG_1    = PROG ; L_i_VAL_EPS_PLAS = PROG ;
    L_i_VAL_TRIAX    = PROG ;
    
	  SI (GTN EGA 1) ;
      L_i_VAL_PORO     = PROG ;
	  FINSI ;
    
    REPE N_PG NBNE ; COMM 'BOUCLE SUR LES POINTS DE GAUSS' ;
		 
*	    LIST (&N_PG) ;
      L_i_VALAX = L_i_VALAX ET (PROG (EXTR AX 'SCAL' 1
      &N_ELT &N_PG)) ;
      SI (((&I) EGA 1) ET ((&N_PG) EGA 1) ET ((&N_ELT) EGA 1)) ;
       COORX_0 = L_i_VALAX ;
      FINSI ;
      L_i_VALAY = L_i_VALAY ET (PROG (EXTR AY 'SCAL' 1
      &N_ELT &N_PG)) ;
		  SI (DIME0 EGA 3) ;
        L_i_VALAZ = L_i_VALAZ ET (PROG (EXTR AZ 'SCAL' 1
        &N_ELT &N_PG)) ;
		  FINSI ;
      L_i_VAL_SIG_YY   = L_i_VAL_SIG_YY   ET 
      (PROG (EXTR SIG_YY 'SMYY' 1 &N_ELT &N_PG)) ;
      
      L_i_VAL_SIG_H    = L_i_VAL_SIG_H    ET 
      (PROG (EXTR SIG_H 'SCAL' 1 &N_ELT &N_PG)) ;
      
	    L_i_VAL_SIG_1    = L_i_VAL_SIG_1    ET 
      (PROG (EXTR SIG_1 'SI11' 1 &N_ELT &N_PG)) ;
      
	    L_i_VAL_EPS_PLAS = L_i_VAL_EPS_PLAS ET 
      (PROG (EXTR EPS_PLAS 'P' 1 &N_ELT &N_PG)) ;
      SI (PP EGA VRAI) ;
      	L_i_VAL_EPS_PLAS = L_i_VAL_EPS_PLAS ET 
        (PROG (EXTR EPS_PLAS 'EPSE' 1 &N_ELT &N_PG)) ;
      FINSI ;
      
      L_i_VAL_TRIAX    = L_i_VAL_TRIAX    ET 
      (PROG (EXTR TAUX_TRIAX 'SCAL' 1 &N_ELT &N_PG)) ;
		  SI (GTN EGA 1) ;
        L_i_VAL_PORO = L_i_VAL_PORO       ET 
        (PROG (EXTR PORO_END 'F' 1 &N_ELT &N_PG)) ;
	    FINSI ;
		       
		  LAX_i = LAX_i ET (PROG (EXTR L_i_VALAX &N_PG)) ;
	    LAY_i = LAY_i ET (PROG (EXTR L_i_VALAY &N_PG)) ;
		  SI(&N_ELT EGA NE) ;  	 
		    SI (DIME0 EGA 3) ;
		      LAZ_i = LAZ_i ET (PROG (EXTR L_i_VALAY &N_PG)) ;
		      P_GAUSS_SELEC = POINT LAX_i LAY_i LAZ_i COUL 'BLEU' ;
          SINON ;
		        P_GAUSS_SELEC = POINT LAX_i LAY_i COUL 'BLEU' ;
		  FINSI ;
*     TRACE (ELT_LIGA ET (P_LIGA COUL 'ROUGE') ET P_GAUSS_SELEC) ;
      FINSI ;  
      SI(&N_ELT EGA NE) ;
        SI ((&N_PG EGA 1) OU (&N_PG EGA 2) OU (&N_PG EGA 3) OU (&N_PG EGA 4)) ;
          N_PGA = &N_PG ;
          SI (DIME0 EGA 3) ;
            N   = 9 ;
		        N_B = 4 ;
            SI (BAS EGA VRAI) ;
              N_PGA = N- &N_PG ;
              SINON ;
                N_PGA = &N_PG ;
            FINSI ;
          FINSI ;
        FINSI ;
        SI (&N_PG EGA 1) ;
	        SIG_YY_gs_1_A   = SIG_YY_gs_1_A     ET 
          (PROG (EXTR SIG_YY 'SMYY' 1 &N_ELT N_PGA)) ;
        
	     	  DEF_PLAS_gs_1_A = DEF_PLAS_gs_1_A   ET 
          (PROG (EXTR EPS_PLAS 'P' 1 &N_ELT N_PGA)) ;
          SI (PP EGA VRAI) ;
            DEF_PLAS_gs_1_A = DEF_PLAS_gs_1_A ET 
           (PROG (EXTR EPS_PLAS 'EPSE' 1 &N_ELT N_PGA)) ;
          FINSI;
        
	     	  TRIAX_gs_1_A    = TRIAX_gs_1_A      ET 
          (PROG (EXTR TAUX_TRIAX 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SI (GTN EGA 1) ;
	     	    PORO_gs_1_A   = PORO_gs_1_A       ET 
            (PROG (EXTR PORO_END 'F' 1 &N_ELT N_PGA)) ;
	     	  FINSI ;
        
	     	  SIG_H_gs_1_A    = SIG_H_gs_1_A      ET 
          (PROG (EXTR SIG_H 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SIG_1_gs_1_A    = SIG_1_gs_1_A      ET 
          (PROG (EXTR SIG_1 'SI11' 1 &N_ELT N_PGA)) ;      

        FINSI ;
        SI (&N_PG EGA 2) ;
	        SIG_YY_gs_2_A   = SIG_YY_gs_2_A     ET 
          (PROG (EXTR SIG_YY 'SMYY' 1 &N_ELT N_PGA)) ;
        
	     	  DEF_PLAS_gs_2_A = DEF_PLAS_gs_2_A   ET 
          (PROG (EXTR EPS_PLAS 'P' 1 &N_ELT N_PGA)) ;
          SI (PP EGA VRAI) ;
            DEF_PLAS_gs_2_A = DEF_PLAS_gs_2_A ET 
           (PROG (EXTR EPS_PLAS 'EPSE' 1 &N_ELT N_PGA)) ;
          FINSI;
        
	     	  TRIAX_gs_2_A    = TRIAX_gs_2_A      ET 
          (PROG (EXTR TAUX_TRIAX 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SI (GTN EGA 1) ;
	     	    PORO_gs_2_A   = PORO_gs_2_A       ET 
            (PROG (EXTR PORO_END 'F' 1 &N_ELT N_PGA)) ;
	     	  FINSI ;
        
	     	  SIG_H_gs_2_A    = SIG_H_gs_2_A      ET 
          (PROG (EXTR SIG_H 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SIG_1_gs_2_A    = SIG_1_gs_2_A      ET 
          (PROG (EXTR SIG_1 'SI11' 1 &N_ELT N_PGA)) ;      

        FINSI ;
        SI (&N_PG EGA 3) ;
	        SIG_YY_gs_3_A   = SIG_YY_gs_3_A     ET 
          (PROG (EXTR SIG_YY 'SMYY' 1 &N_ELT N_PGA)) ;
        
	     	  DEF_PLAS_gs_3_A = DEF_PLAS_gs_3_A   ET 
          (PROG (EXTR EPS_PLAS 'P' 1 &N_ELT N_PGA)) ;
          SI (PP EGA VRAI) ;
            DEF_PLAS_gs_3_A = DEF_PLAS_gs_3_A ET 
           (PROG (EXTR EPS_PLAS 'EPSE' 1 &N_ELT N_PGA)) ;
          FINSI;
        
	     	  TRIAX_gs_3_A    = TRIAX_gs_3_A      ET 
          (PROG (EXTR TAUX_TRIAX 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SI (GTN EGA 1) ;
	     	    PORO_gs_3_A   = PORO_gs_3_A       ET 
            (PROG (EXTR PORO_END 'F' 1 &N_ELT N_PGA)) ;
	     	  FINSI ;
        
	     	  SIG_H_gs_3_A    = SIG_H_gs_3_A      ET 
          (PROG (EXTR SIG_H 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SIG_1_gs_3_A    = SIG_1_gs_3_A      ET 
          (PROG (EXTR SIG_1 'SI11' 1 &N_ELT N_PGA)) ;      

        FINSI ;
        SI (&N_PG EGA 4) ;
	        SIG_YY_gs_4_A   = SIG_YY_gs_4_A     ET 
          (PROG (EXTR SIG_YY 'SMYY' 1 &N_ELT N_PGA)) ;
        
	     	  DEF_PLAS_gs_4_A = DEF_PLAS_gs_4_A   ET 
          (PROG (EXTR EPS_PLAS 'P' 1 &N_ELT N_PGA)) ;
          SI (PP EGA VRAI) ;
            DEF_PLAS_gs_4_A = DEF_PLAS_gs_4_A ET 
           (PROG (EXTR EPS_PLAS 'EPSE' 1 &N_ELT N_PGA)) ;
          FINSI;
        
	     	  TRIAX_gs_4_A    = TRIAX_gs_4_A      ET 
          (PROG (EXTR TAUX_TRIAX 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SI (GTN EGA 1) ;
	     	    PORO_gs_4_A   = PORO_gs_4_A       ET 
            (PROG (EXTR PORO_END 'F' 1 &N_ELT N_PGA)) ;
	     	  FINSI ;
        
	     	  SIG_H_gs_4_A    = SIG_H_gs_4_A      ET 
          (PROG (EXTR SIG_H 'SCAL' 1 &N_ELT N_PGA)) ;
          
	     	  SIG_1_gs_4_A    = SIG_1_gs_4_A      ET 
          (PROG (EXTR SIG_1 'SI11' 1 &N_ELT N_PGA)) ;      

        FINSI ;

      FINSI ; 
      
    FIN N_PG ;  
		SI (DIME0 EGA 3) ;
		 N   = 9 ;
		 N_B = 4 ;
		 SINON ;
		  N   = 5 ;
			N_B = 2 ;
		FINSI ;
		
		REPETER N_PG_SELEC (N_B) ;
		  SI (BAS EGA VRAI) ;
         N_PGS = N- &N_PG_SELEC ;
         SINON ;
         N_PGS = &N_PG_SELEC ;
      FINSI ;
		   LAX     = LAX ET (PROG 
        (EXTR L_i_VALAX (N_PGS))) ;
   
       LAX0    = LAX0 ET COORX_0 ;

		   LAY     = LAY ET (PROG 
        (EXTR L_i_VALAY (N_PGS))) ; 
		   SI (DIME0 EGA 3) ;
		       LAZ = LAZ ET 
            (PROG (EXTR L_i_VALAZ (N_PGS))) ;
		   FINSI ;
		   
		   LSIG_YY   = LSIG_YY   ET 
        (PROG (EXTR L_i_VAL_SIG_YY (N_PGS))) ;
		   LSIG_H    = LSIG_H    ET 
        (PROG (EXTR L_i_VAL_SIG_H (N_PGS))) ;
		   LSIG_1    = LSIG_1    ET 
        (PROG (EXTR L_i_VAL_SIG_1 (N_PGS))) ;
		   LEPS_PLAS = LEPS_PLAS ET 
        (PROG (EXTR L_i_VAL_EPS_PLAS (N_PGS))) ;
		   LTRIAX    = LTRIAX    ET 
        (PROG (EXTR L_i_VAL_TRIAX (N_PGS))) ;
		   SI (GTN EGA 1) ;
		     LPORO = LPORO       ET 
          (PROG (EXTR L_i_VAL_PORO (N_PGS))) ;
		   FINSI ;
*		   LIST (LPORO) ;
       
		 SI (DIME0 EGA 3) ;
		   P_GAUSS_SELEC2     = POINT LAX LAY LAZ COUL 'BLEU' ;
		  SINON ;
		    P_GAUSS_SELEC2 = POINT LAX LAY COUL 'BLEU' ;
		 FINSI ;
*    TRACE (ELT_LIGA ET (P_LIGA COUL 'ROUGE') ET P_GAUSS_SELEC2) ;
		 
		FIN N_PG_SELEC ;
    
  FIN N_ELT ;
	SI(DIME0 EGA 2) ;
    P_GAUSS_SELEC = POINT LAX LAY COUL 'BLEU' ;
    SINON ; 
      P_GAUSS_SELEC = POINT LAX LAY LAZ COUL 'BLEU' ;
	FINSI ;
* TRACE (ELT_LIGA ET (P_LIGA COUL 'ROUGE') ET P_GAUSS_SELEC) ;		  			 	 

  TABLE_SORTIE.'SIG_H'.&I    = LSIG_H ;
  TABLE_SORTIE.'SIG_1'.&I    = LSIG_1 ;
	TABLE_SORTIE.'DEF_PLAS'.&I = LEPS_PLAS ;
	TABLE_SORTIE.'TRIAX'.&I    = LTRIAX ;
	SI (GTN EGA 1) ;
	  TABLE_SORTIE.'PORO'.&I   = LPORO ;
	FINSI ;

	TABLE_SORTIE.'SIG_YY'.&I   = LSIG_YY ;
	TABLE_SORTIE.'COOR_X'.&I   = LAX-LAX0 ;

	SI (DIME0 EGA 3);
	  TABLE_SORTIE.'COOR_Z'.&I = LAZ ;
	FINSI ;  
FIN I ;

*VISUALISATION DES CHAMPS AUX POINTS DE GAUSS
*********************************************

OPTI TRAC 'PSC' ;                  
OPTI 'FTRA' 'SIGYY.ps' ;
REPE I (TAILLE) ;
  DEFOT_I  = DEPLACEMENT0.&I ;
  DEFO_I   = DEFO DEFOT_I (MAILO.&I) 1. ;
*SIGMA YY SOIT SIGMA OUVERTURE
   SIG_YY0 = REDU (EXCO (CONTRAINTE0. &I) 
   'SMYY') (MAILO.&I) ;
	 VISUPG (MODO.&I)SIG_YY0 'SMYY' ECHSIGY TABV DEFOT_I 
  'SIGyy PG' ;
FIN I ;

SI (GTN EGA 1) ;
  OPTI TRAC 'PSC' ; 
  OPTI 'FTRA' 'PORO.ps' ; 
  REPE I (TAILLE) ;
*VAR INTERNE D ENDOMMAGEMENT 'F'
    DEFOT_I  = DEPLACEMENT0.&I ;
    DEFO_I   = DEFO DEFOT_I (MAILO.&I) 1. ;
    PORO_END0 = REDU (EXCO (VAR_INTERNE0. &I) 'F') 
               (MAILO.&I) ;
	  VISUPG (MODO.&I) PORO_END0 'F' ECHPORO TABV DEFOT_I 
   'Porosité PG' ; 
  FIN I ;
  
  OPTI TRAC 'PSC' ;                  
  OPTI 'FTRA' 'BROKEN.ps' ;
  REPE I (TAILLE) ;
*VAR INTERNE D ENDOMMAGEMENT 'BROK'
    DEFOT_I  = DEPLACEMENT0.&I ;
    DEFO_I   = DEFO DEFOT_I (MAILO.&I) 1. ;
    BROK_END0 = REDU (EXCO (VAR_INTERNE0. &I) 'BROK') 
               (MAILO.&I) ;
	  VISUPG (MODO.&I) BROK_END0 'BROK' ECHBROK TABV DEFOT_I 
   'Broken PG' ;
  FIN I ;   
FINSI ;

OPTI TRAC 'PSC' ;                  
OPTI 'FTRA' 'DEFO_PLAS.ps' ; 
REPE I (TAILLE) ;
  DEFOT_I  = DEPLACEMENT0.&I ;
  DEFO_I   = DEFO DEFOT_I (MAILO.&I) 1. ;
*VAR INTERNE D ECROUISSAGE 'P/EPSE'
  EPS_PLAS0 = REDU (EXCO (VAR_INTERNE0. &I) 'P')
  (MAILO.&I) ;
  SI (PP EGA VRAI) ;
    EPS_PLAS0 = REDU (EXCO (VAR_INTERNE0. &I) 'EPSE')
              (MAILO.&I) ;
    VISUPG (MODO.&I) EPS_PLAS0 'EPSE' ECHPLAS TABV DEFOT_I 
   'Defo_plas PG' ;	
    SINON ;
      VISUPG (MODO.&I) EPS_PLAS0 'P' ECHPLAS TABV DEFOT_I 
      'Defo_plas PG' ;
  FINSI ;
FIN I ;	   

OPTI TRAC 'PSC' ;                  
OPTI 'FTRA' 'VONMISES.ps' ;
REPE I (TAILLE) ;
  DEFOT_I  = DEPLACEMENT0.&I ;
  DEFO_I   = DEFO DEFOT_I (MAILO.&I) 1. ;
*CONTRAINTE EQUIVALENTE DE VONMISES
  SIG_VM0   = VMIS (MODO.&I) (CONTRAINTE0. &I) ;
	VISUPG (MODO.&I) SIG_VM0 'SCAL' ECHVONM TABV DEFOT_I 'VON MISES PG' ;
FIN I ;	
	  
*FIN DE LA PARTIE VISUALISATION
*******************************

REPE J (4) ;
 SI ((&J) EGA 1) ;
  TABLE_SORTIE.'SIG_H_PG'.&J    = SIG_H_gs_1_A ;
  TABLE_SORTIE.'SIG_1_PG'.&J    = SIG_1_gs_1_A ;
  TABLE_SORTIE.'DEF_PLAS_PG'.&J = DEF_PLAS_gs_1_A ;
  TABLE_SORTIE.'TRIAX_PG'.&J    = TRIAX_gs_1_A ;
  SI (GTN EGA 1) ;
   TABLE_SORTIE.'PORO_PG'.&J    = PORO_gs_3_A ;
  FINSI ;
  TABLE_SORTIE.'SIG_YY_PG'.&J   = SIG_YY_gs_1_A ;

 FINSI ;
 SI ((&J) EGA 2) ;
  TABLE_SORTIE.'SIG_H_PG'.&J    = SIG_H_gs_2_A ;
  TABLE_SORTIE.'SIG_1_PG'.&J    = SIG_1_gs_2_A ;
  TABLE_SORTIE.'DEF_PLAS_PG'.&J = DEF_PLAS_gs_2_A ;
  TABLE_SORTIE.'TRIAX_PG'.&J    = TRIAX_gs_2_A ;
  SI (GTN EGA 1) ;
   TABLE_SORTIE.'PORO_PG'.&J    = PORO_gs_2_A ;
  FINSI ;
  TABLE_SORTIE.'SIG_YY_PG'.&J   = SIG_YY_gs_2_A ;

 FINSI ;
 SI ((&J) EGA 3) ;
  TABLE_SORTIE.'SIG_H_PG'.&J    = SIG_H_gs_3_A ;
  TABLE_SORTIE.'SIG_1_PG'.&J    = SIG_1_gs_3_A ;
  TABLE_SORTIE.'DEF_PLAS_PG'.&J = DEF_PLAS_gs_3_A ;
  TABLE_SORTIE.'TRIAX_PG'.&J    = TRIAX_gs_3_A ;
  SI (GTN EGA 1) ;
   TABLE_SORTIE.'PORO_PG'.&J    = PORO_gs_3_A ;
  FINSI ;
  TABLE_SORTIE.'SIG_YY_PG'.&J   = SIG_YY_gs_3_A ;
 FINSI ;
 SI ((&J) EGA 4) ;
  TABLE_SORTIE.'SIG_H_PG'.&J    = SIG_H_gs_4_A ;
  TABLE_SORTIE.'SIG_1_PG'.&J    = SIG_1_gs_4_A ;
  TABLE_SORTIE.'DEF_PLAS_PG'.&J = DEF_PLAS_gs_4_A ;
  TABLE_SORTIE.'TRIAX_PG'.&J    = TRIAX_gs_4_A ;
  SI (GTN EGA 1) ;
   TABLE_SORTIE.'PORO_PG'.&J     = PORO_gs_4_A ;
  FINSI ;
  TABLE_SORTIE.'SIG_YY_PG'.&J   = SIG_YY_gs_4_A ;

 FINSI ;

FIN J ;

*SORTIE EXCEL DES CHAMPS AUX POINTS DE GAUSS SUR LE LIGAMENT
OPTI SORT 'SIG_H' ;
SORT EXCE TABLE_SORTIE.'SIG_H' ; 

OPTI SORT 'SIG_1' ;
SORT EXCE TABLE_SORTIE.'SIG_1' ;   

OPTI SORT 'DEF_PLAS' ;
SORT EXCE TABLE_SORTIE.'DEF_PLAS' ;

OPTI SORT 'TRIAX' ;
SORT EXCE TABLE_SORTIE.'TRIAX' ;   

OPTI SORT 'SIG_YY' ;
SORT EXCE TABLE_SORTIE.'SIG_YY' ;   

SI (GTN EGA 1) ;
     OPTI SORT 'PORO' ;
     SORT EXCE TABLE_SORTIE.'PORO' ;    
FINSI ; 

*SORTIE EXCEL DES CHAMPS AUX POINTS DE GAUSS SUR LE LIGAMENT
OPTI SORT 'SIG_H_PG' ;
SORT EXCE TABLE_SORTIE.'SIG_H_PG' ; 

OPTI SORT 'SIG_1_PG' ;
SORT EXCE TABLE_SORTIE.'SIG_1_PG' ;   

OPTI SORT 'DEF_PLAS_PG' ;
SORT EXCE TABLE_SORTIE.'DEF_PLAS_PG' ;

OPTI SORT 'TRIAX_PG' ;
SORT EXCE TABLE_SORTIE.'TRIAX_PG' ;   

OPTI SORT 'SIG_YY_PG' ;
SORT EXCE TABLE_SORTIE.'SIG_YY_PG' ;   

SI (GTN EGA 1) ;
     OPTI SORT 'PORO_PG' ;
     SORT EXCE TABLE_SORTIE.'PORO_PG' ;    
FINSI ; 

*EVOLUTIONS DES LA FORCE - DEPLACEMENT 
F_CMOD = EVOL MANU 'FLECHE' LDEPL 'FORCE' LFORC ;

OPTI SORT 'FORCE' ;
SORT EXCE LFORC ;
OPTI SORT 'F_CMOD' ;
SORT EXCE F_CMOD ;
