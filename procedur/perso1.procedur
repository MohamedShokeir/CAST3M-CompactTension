***********************************************************
*                   ProcÃ©dure PERSO1                      *
***********************************************************

$$$$ PERSO1

DEBP PERSO1 TAB1*'TABLE' ;
*
MO1   = TAB1.WTABLE.'MO_TOT' ;
MA1   = TAB1.WTABLE.'MA_TOT' ;
MAIL1 = 'EXTRAIRE' MO1 'MAIL' ;
*
SI (GTN EGA 1 ) ;
     TAB1.'MO_ACT'.(TAB1.'J')  = MO1 ;
     TAB1.'MAT_ACT'.(TAB1.'J') = MA1 ;
     TAB1.'MAI_ACT'.(TAB1.'J') = 'EXTRAIRE' MO1 'MAIL' ;		
FINSI ;	
*
************************************
*       SAUVEGARDE EN VTK          *
************************************

'SI'( ('DIME' TAB1.DEPLACEMENTS) > 1) ;
        
*  SVTK (TAB1.'ESTIMATION') MO1 'CT_node' TAB1.'IND';
  VISUPG MO1 (SVTK (TAB1.'ESTIMATION') MO1 'CT' TAB1.'IND');
  TAB1.'IND' = TAB1.'IND' '+' 1 ;
		
  'SI' ('EXISTE' LTPC (TAB1.WTABLE.TEMPS0) 1.e-6) ;  

*    SVTK (TAB1.'ESTIMATION') MO1 'CT_node' TAB1.'IND' ;
    VISUPG MO1 (SVTK (TAB1.'ESTIMATION') MO1 'CT' TAB1.'IND') ;
    TAB1.'IND' = TAB1.'IND' '+' 1 ;
	 
  'FINSI' ;
   
FINSI ;

*
**********************************************************
*                  FORCE - DEPLACEMENT                   *
**********************************************************
*
*MESS 'DEBUT FORCE - DEPLACEMENT' ;
*
'SI'( ('DIME' TAB1.DEPLACEMENTS) > 1) ;

  SI (DIME0 EGA 3);
    TMP1 = 'EXTRAIRE' TAB1.ESTIMATION.DEPLACEMENTS 'UY' (CMOD1) ;
  SINON ;
    TMP1 = 'EXTRAIRE' TAB1.ESTIMATION.DEPLACEMENTS 'UY' (CMOD) ;
  FINSI ;
    TMP2 = 'EXCO' TAB1.ESTIMATION.REACTIONS 'FY' ;
    UN = 'MANUEL' CHPO CHARn 1 'SCAL' 1. ;
    TMP2 = ABS('XTY' TMP2 UN ('MOTS' 'SCAL') ('MOTS' 'SCAL')) ;
    SI (DIME0 'EGA' 3);
      TAB1.DEP = TAB1.DEP 'ET' ('PROG' TMP1*2.) ;
      TAB1.FOR = TAB1.FOR 'ET' ('PROG' TMP2*2.) ;
    SINON;
      TAB1.DEP = TAB1.DEP 'ET' ('PROG' TMP1) ;
      TAB1.FOR = TAB1.FOR 'ET' ('PROG' TMP2) ;
    FINSI;
    F = EXTR (TAB1.FOR) (DIME (TAB1.FOR)) ;
    FMAX = MAXI (TAB1.FOR) ;

   'OPTION' 'SORTIR' 'force_cmod.csv' ;
   'SORTIR' EXCE TAB1.DEP TAB1.FOR SEPA TABU ;

FINSI ;

*
*MESS 'FIN FORCE - DEPLACEMENT' ;
*
SI (GTN EGA 1) ;

************************************************************
*           DEFINITION DES CHAMPS PAR ELEMENTS             *
************************************************************

     SI (DIME0 EGA 3) ;
       CHX CHY CHZ = COOR Lg0;
       LIGNELIGA3 = POINT CHX 'INFERIEUR' (a_0+a_f);
*         LIGNELIGA3 = MAIL1 POINT 'PLAN' (0. 0. 0.) (1. 0. 0.) (0. 0. 1) 1.E-9 ;
     	 LIGA       = MAIL1 ELEM 'APPUYE' 'LARGEMENT' LIGNELIGA3 ;
     	 SINON ;
             LIGA   = MAIL1 ELEM APPUYE LARGEMENT LIGNELIGA ;
     FINSI ;

*    LIST (NBEL (LIGA)) ;

*EXTRACTION DE SIGMA YY SOIT SIGMA OUVERTURE
*-------------------------------------------
     SIG_YYL   = REDU (EXCO (TAB1.ESTIMATION.CONTRAINTES) 'SMYY') LIGA ;

*EXTRACTION DE LA VAR INTERNE D ECROUISSAGE P
*--------------------------------------------
     EPS_PLASL = REDU (EXCO (TAB1.ESTIMATION.VARIABLES_INTERNES) 'P') LIGA ;


*EXTRACTION DE LA VAR INTERNE D ENDOMMAGEMENT F
*----------------------------------------------
     PORO_ENDL = REDU (EXCO (TAB1.ESTIMATION.VARIABLES_INTERNES) 'F') LIGA ;



*EXTRACTION DE LA VARIABLE D ENDOMMAGEMENT BROKEN
*------------------------------------------------
     BROK_ENDL = REDU (EXCO (TAB1.ESTIMATION.VARIABLES_INTERNES) 'BROK') LIGA ;

*
*******************************************************************************************
*      CALCUL DE LA POROSITE MOYENNE SUR LE LIGAMENT ET SON EVOL. ENTRE 2 PAS DE TEMPS    *
*******************************************************************************************
*
*MESS 'DEBUT PORO MOY. ET VARIA PORO LIGAMENT' ;
*
* NOUVEAU MODELE ASSOCIE AUX MAILLES DE LA LIGNE INF. DU LIGA *
*--------------------------------------------------------------
     MOLIGA = REDU MO1 LIGA ;
     TMP2   = 'EXCO' TAB1.ESTIMATION.VARIABLES_INTERNES 'F' ;

* VALEUR MOYENNE DE LA POROSITE SUR TOUTES LES MAILLES DE LIGNELIGA *
*--------------------------------------------------------------------
     MCAV2 = INTG MOLIGA TMP2 ;

*    LIST (MCAV2) ;

* VALEUR DE LA POROSITE SUR LES MAILLES APPUYES LIGNE INF *
*----------------------------------------------------------

     SI (((TAB1.ESTIMATION.TEMPS) EGA (EXTR LTPC 1))) ;

* VALEUR INITIALE DE POROSITE, FIXEE PAR L UTILISATEUR *
*-------------------------------------------------------
         MCAV1    = F00 ;

*        LIST (MCAV1) ;

* CREATION D UNE LISTE POUR L EVOLUTION DE F *
*---------------------------------------------
         LMCAV1   = PROG ;

* CREATION D UNE LISTE POUR L EVOL. DE LA VARIATION REALITIVE DE F *
*-------------------------------------------------------------------
         LVARCAV1 = PROG ;

         SINON ;
	   
* CREATION D UNE TABLE A PARTIR DU PAS SUPERIEUR A 1 *
*-----------------------------------------------------
             LMCAV1   = TAB1.LMCAV ;
             LVARCAV1 = TAB1.LVARMCAV ;
	         MCAV1    = (EXTR LMCAV1 (DIME LMCAV1)) ;
	
     FINSI ;


* CREATION D UN CHAMP PAR POINT DE TYPE SCAL A PARTIR LES MAILLES LIGNELIGA *
*----------------------------------------------------------------------------
     FONC1X = 'MANU' 'CHPO' LIGA 1 'SCAL' 1. ;

* TRANSFORMATION EN CHAMP PAR ELEMENT SUR LE MODELE REDUIT AU LIGA, APPLIQ AU CENTRE DE GRAVITE *
*-------------------------------------------------------------------------------------------------
     FONC1  = 'CHAN' 'CHAM' MOLIGA FONC1X 'GRAVITE' 'SCALAIRE' ;

* MOYENNER CE CHAMP PAR ELEMENT SUR TOUTES LES MAILLES DE LIGNELIGA *
*---------------------------------------------------------------------
     V_FIS  = 'INTG' MOLIGA FONC1 ;

*    LIST (V_FIS) ;

* NORMALISATION DE LA POROSITE MOYENNES *
*----------------------------------------
     MCAV2  = MCAV2 / V_FIS ;

*    LIST (MCAV2) ;

* VARIATION RELATIVE DE LA PORO MOYENNE ENTRE DEUX PAS DE TEMPS *
*-----------------------------------------------------------------
     VARIA  = ((MCAV2 - MCAV1) / MCAV1) ;

*    LIST (VARIA) ;
* LISTE DE L EVOL DE PORO PRECEDENTE ( DEBUT MCAV2) *
*----------------------------------------------------
     LMCAV1 = LMCAV1 ET (PROG MCAV2) ;

* LISTE DE LA VARIATION RELATIVE DE PORO ENTRE DEUX PAS DE TEMPS *
*------------------------------------------------------------------
     LVARCAV1 = LVARCAV1 ET (PROG VARIA) ;

     TAB1.LMCAV = LMCAV1 ;
     TAB1.LVARMCAV = LVARCAV1 ;
*
	
* NOMBRE DE POINTS DE GAUSS *
*----------------------------
  SI (DIME0 EGA 3);
    NBPG = 8 ;
  SINON ;
    NBPG = 4 ;
  FINSI ;
  NELEM     = NBEL LIGA ;
*    LIST (NELEM) ;
  CHMFISS   = MANU 'CHAM' MOLIGA 'POSI' GRAVITE SCAL 1 1 0. ;
  NBPGCAST  = 0 ;
  NBPGCAST0 = 0 ;
  NBPGCAST1 = 0 ;

**************************************************
* SUPRESSION DES ELEMENTS CASSES SUR LE LIGAMENT *
**************************************************
*
* BOUCLE LES TOUTES LES MAILLES DU LIGNE LIGA *
*----------------------------------------------
     REPE IND1 NELEM ;
         NPGCASSE = 0. ;
** BOUCLE SUR LES POINTS DE GAUSS D UN ELEMENT FINI *
*-----------------------------------------------------
         REPE IND2 NBPG ;
** EXTRACTION DE LA PORO SUR LE POINT DE GAUSS IND2 DE L ELEMENT FINI IND1 *
*----------------------------------------------------------------------------
             VALF = EXTR PORO_ENDL 'F' 1 (&IND1) (&IND2) ;
             VALP = EXTR EPS_PLASL 'P' 1 (&IND1) (&IND2) ;
             VALB = EXTR BROK_ENDL 'BROK' 1 (&IND1) (&IND2) ;
**		     LIST (VALF) ;
**		     LIST (VALP) ;
**		     LIST (VALB) ;
             COUPURE = 0.9 ;
			 
*            SI (((VALF > (COUPURE * FR0)) ET ((VALB EGA 0.5) OU (VALB > 0.5))) OU (VALP > 0.5));
             SI (((VALB EGA 0.5) OU (VALB > 0.5)) OU (VALP > 0.5));
                     NPGCASSE  = NPGCASSE + 1 ;
     			     NBPGCAST1 = NBPGCAST1 + 1 ;
					 
				     SI (NPGCASSE > 1) ; 
         		    		 NBPGCAST2 = NPGCASSE ;
				     FINSI ;
				   
             FINSI ;

         FIN IND2 ;
		  
         SI (NPGCASSE > 1) ; 
                 NBPGCAST0 = NBPGCAST0 + NBPGCAST2 ;
         FINSI ;
  
** CREATION D UN CHAMP PAR ELEMENT APPLIQUE AU CENTRE DE GRAVITE DE LA MAILLE IND1 
** AYANT NPGCASSE POINTS DE GAUSS CASSES *
*---------------------------------------------------------------------------------
         CHFISS1 = MANU 'CHAM' MOLIGA 'POSI' GRAVITE SCAL &IND1 1 NPGCASSE ;
         CHMFISS = CHMFISS + CHFISS1 ;
		  
     FIN IND1 ;

     SI (DIME0 EGA 3) ;
	         NPGCRIT = 4 ;
			 SINON ;
                 NPGCRIT = 2 ;
	 FINSI ;

     MAILREST    = CHMFISS ELEM 'INFERIEUR' (NPGCRIT) ;
     LIST (NBEL (MAILREST)) ;
     
     MAILCAST    = DIFF LIGA MAILREST ;
     NBRO        = NBEL (MAILCAST) ;
     TAB1.'NBRO' = TAB1.'NBRO' '+' NBRO ;
     NEBROKEN    = TAB1.'NBRO' ;
     
     TAB1.'BROO' = TAB1.'BROO' '+' NBPGCAST0 ; 
     NPBROKEN    = TAB1.'BROO' ;
     
     MESO1       = DIFF MAIL1 MAILCAST ;
     MESO2       = DIFF ENTA MAILCAST ;
     LIST (NBEL (ENTA)) ;
     LIST (NBEL (MESO2)) ;

*
* MISE A JOUR DU MODELE ET DU MATERIAU
*-------------------------------------
*
     SI (NBRO > 0);
         MO2                    = 'REDU' MO1 MESO1 ;
         TAB1.WTABLE.'MO_TOT'   = MO2 ;
         TAB1.WTABLE.'MO_TOTAL' = MO2 ;
         TAB1.WTABLE.'MOD_MEC'  = MO2 ;
                  
         MA2                    = 'REDU' MA1 MESO1 ;
         TAB1.WTABLE.'MA_TOT'   = MA2 ; 
         TAB1.WTABLE.'MA_TOTAL' = MA2 ;      
         TAB1.WTABLE.'MAT_MEC'  = MA2 ;
     	 SINON ;
     	     MO2                    = MO1 ; 
     		 TAB1.WTABLE.'MO_TOT'   = MO2 ; 
     		 TAB1.WTABLE.'MO_TOTAL' = MO2 ; 
     		 TAB1.WTABLE.'MOD_MEC'  = MO2 ; 
     		           
     		 MA2                    = MA1 ; 
     		 TAB1.WTABLE.'MA_TOT'   = MA2 ;  
     		 TAB1.WTABLE.'MA_TOTAL' = MA2 ;       
     		 TAB1.WTABLE.'MAT_MEC'  = MA2 ; 
     
     FINSI ;
     		  
     TAB1 . 'TOTPTCAS' = NPBROKEN ; 
     TAB1 . 'TOTECAS'  = NEBROKEN ; 

*******************************************************
* IMPRESSION DES DONNEES A L ECRAN A CHAQUE ITERATION *
*******************************************************
*
MESS '**********************************************************************' ;
MESS '** Nombre total d elements du maillage initial :' (NBEL (CTTOT)) ;
MESS '** Nombre total d elements du maillage apres endo. :' (NBEL (MESO1)) ;
MESS '** Average porosity :' MCAV2 ;
MESS '** Previous value of porosity :' MCAV1 ;
MESS '** Relative variation of f :' VARIA ;                                       
MESS '** Broken Gauss points tot :' NPBROKEN ;
MESS '** Broken elements tot :' NEBROKEN ;
MESS '** Nombre de pts Gauss casses par pas de temps :' NBPGCAST1 ;
MESS '** elements casses a chaque pas de temps :' NBRO ;	
MESS '** Resultant force (N): ' F ;
MESS '** Max force (N): ' FMAX ;
MESS '************************************************************************' ;


*
     TAB1.'MAREACTIONS'.(TAB1.'J')     = TAB1.'ESTIMATION'.'REACTIONS' ;
     TAB1.'MACONTRAINTES'.(TAB1.'J')   = TAB1.'ESTIMATION'.'CONTRAINTES' ;
     TAB1.'MESVAR_INTERNES'.(TAB1.'J') = TAB1.'ESTIMATION'.'VARIABLES_INTERNES' ;
     TAB1.'MONDEPLACEMENTS'.(TAB1.'J') = TAB1.'ESTIMATION'.'DEPLACEMENTS' ;
*
     TAB1.'J' = TAB1.'J' + 1 ;
*
*********************************************************
* RESTE D ELEMENTs SUR LIGNELIGA POUR L ARRET DU CALCUL	*
*********************************************************
*
     SI (GTN EGA 1) ;
	     SI (DIME0 EGA 3) ;
             MAISP = 'EXTRAIRE' MO2 'MAIL' ;
             LGA   = MAISP ELEM APPUYE LARGEMENT LIGNELIGA3 ;
			 SINON ;
			     MAISP = 'EXTRAIRE' MO2 'MAIL' ;
			     LGA   = MAISP ELEM APPUYE LARGEMENT LIGNELIGA ;
		 FINSI ;
	 FINSI ;
*
**************************************************
*  ESTIMATION DE L INCREMENT MAXIMAL DE POSOSITE *
**************************************************
*MESS 'DEBUT DE L ESTIMATION DE L INCREMMENT DE PORO' ;

     'SI'(('DIME' TAB1.DEPLACEMENTS) > 1) ;
     
              TMP  =  TAB1.'F_PREC';
              TMP  = CHAN TMP MODTOT 'STRESSES' ;
              TMP2 = 'EXCO' TAB1.ESTIMATION.VARIABLES_INTERNES 'F' ;
              TMP2 = CHAN TMP2 MODTOT 'STRESSES' ;
              TMP3 = 'EXCO' TAB1.ESTIMATION.VARIABLES_INTERNES 'BROK' ;
              TMP3 = CHAN TMP3 MODTOT 'STRESSES' ;
     	     
     	     
              TMP  = 'REDU' TMP MESO1 ;
              TMP2 = 'REDU' TMP2 MESO1 ;
              TMP3 = 'REDU' TMP3 MESO1 ;
     	     
              TMPA  = ('NOMC' TMP 'SCAL')*('NOMC' (1 '-' TMP3) 'SCAL') ;
              TMP2A = ('NOMC' TMP2 'SCAL')*('NOMC' (1 '-' TMP3) 'SCAL') ;
     	     
              FP = 'MAXIMUM' TMPA ;
              FA = 'MAXIMUM' TMP2A ;
              DF = 'ABS'(FA '-' FP) ;
     	     
              TAB1.'F_PREC' = TMP2 ;
     	     
              'SINON' ;
     	     
              DF = 0 ;
     
     'FINSI' ;

**'MESSAGE' TAB1.'NBRO' ;
**'MESSAGE' DF ;

     TAB1.WTABLE.'DF' = DF ;
     TAB1.WTABLE.'DFMAX' = TAB1.'DFMAX' ;

*MESS 'FIN DE L ESTIMATION DE L INCREMMENT DE PORO' ;

FINSI ;


************************************
*       SAUVEGARDE DES DONNEES     *
************************************

*MESS 'DEBUT SAUVERGADE DES DONNEES' ;

SI (('DIME' TAB1.DEPLACEMENTS) > 1) ;
  OPTI SAUV 'CT_calc1.sauv' ;
  SAUV TAB0 TAB1 CHARn CMOD CMOD1 MODTOT MATTOT DIME0 CHA0 ALPHA  
  LTPS LTPC GOUP ENTA ENTA_CT LIGNELIGA CTTOT MOD1 MOD2 MAT1 MAT2 
  YOUN0 YOUN1 NU RHO0 RHO1 BLM4Y BLM1Z BLM2Y BLM3X MAT3 MOD3 VOCE  
  PHI MODIN CHA1 CLM F00 FR0 MODTOT MATTOT;	

  SI (GTN 'EGA' 1);
    SI ((((NBEL (LGA)) < 4) OU ((NBEL (LGA)) EGA 4)) 
      ET (MCAV2 > (F00*2.))) ; 
      FIN ; 
    FINSI; 
  FINSI;
 
FINSI; 

*
*MESS 'FIN SAUVERGADE DES DONNEES' ;
*

	 
'FINPROC' ;

$$$$
