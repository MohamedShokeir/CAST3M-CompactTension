***********************************************************
* Proc√©dures VTK
***********************************************************

$$$$ SVTK

'DEBPROC' SVTK TABNL*TABLE OBJMO*MMODEL FICH*MOT
IND*ENTIER RMAIL/MAILLAGE;

DIME3D = FAUX ;

MAILL = 'EXTRAIRE' OBJMO 'MAIL';
UN = 'MANUEL' CHML OBJMO SCAL 1. 'GRAVITE';
FVOLU = (('INTG' 'ELEM' UN OBJMO)'**'(-1));

VM = VMIS TABNL.CONTRAINTES OBJMO;
VM = 'NOMC' 'VMIS' VM;
CHAT = ('REDU' OBJMO TABNL.CONTRAINTES) 'ET'
 ('REDU' OBJMO TABNL.VARIABLES_INTERNES)
  'ET' ('REDU' OBJMO VM);

TABV = TABLE;
LCOM = 'EXTRAIRE' CHAT 'COMP';

'REPETER' BO2 ('DIME' LCOM);
'SI' ('EGA' ('EXTRAIRE' &BO2 LCOM) 'EPSE');
'ITERER' BO2;
'FINSI';

TMP1 = 'INTG' 'ELEM' OBJMO
 CHAT ('EXTRAIRE' &BO2 LCOM);
TMP2 = 'CHANGER' TMP1 OBJMO 'GRAVITE';
TMP3 = 'NOMC' (TMP2*FVOLU) ('EXTRAIRE' &BO2 LCOM);
TABV.('EXTRAIRE' &BO2 LCOM) = TMP3;
'FIN' BO2;

DEPUX = 'EXCO' TABNL.DEPLACEMENTS 'UX';
DEPUY = 'EXCO' TABNL.DEPLACEMENTS 'UY';
DEPUZ = 0 * DEPUY;

SI (DIME3D EGA VRAI);
DEPUZ = 'EXCO' TABNL.DEPLACEMENTS 'UZ';
FINSI ;

DEPT = ('NOMC' 'UX' DEPUX) 'ET' ('NOMC' 'UY' DEPUY)
'ET' ('NOMC' 'UZ' DEPUZ);


'OPTION' 'SORTIR' FICH;
'SORTIR' VTK MAILL 'MAIL' DEPT 'DEPL'
 TABV 'FORM' 'AUTO' 'TEMP' IND 'DOUBLE_PRECISION';

'SI' ('EXISTE' RMAIL);
GCHA1 = 'REDU' CHAT RMAIL;
GCHA2 = 'REDU' DEPT RMAIL;
GMODL = 'REDU' OBJMO RMAIL; 

G1 G2 G3 = GVTK GCHA1 GCHA2 GMODL;

'OPTION' 'SORTIR' ('CHAINE' FICH '_POINTS_DE_GAUSS');
'SORTIR' VTK G1 'GAUS' G2 'DEPL'
 G3 'FORM' 'AUTO' 'TEMP' IND 'DOUBLE_PRECISION';
'FINSI';

'FINPROC';

$$$$ GVTK

'DEBPROC' GVTK CHAM0*MCHAML CHPO0*CHPOINT OBJMO*MMODEL;

MAILL = 'EXTRAIRE' OBJMO 'MAIL';

***************************************************

CHA1 = 'CHANGER' CHAM ('COORDONNEE' 1 MAILL) OBJMO 'STRESSES';
CHA1 = 'NOMC' 'X' CHA1;
CHA2 = 'CHANGER' CHAM ('COORDONNEE' 2 MAILL) OBJMO 'STRESSES';
CHA2 = 'NOMC' 'Y' CHA2;
CHAP = CHA1 'ET' CHA2;

SI (DIME3D EGA VRAI) ;
CHA3 = 'CHANGER' CHAM ('COORDONNEE' 3 MAILL) OBJMO 'STRESSES';
CHA3 = 'NOMC' 'Z' CHA3;
CHAP = CHA1 'ET' CHA2 'ET' CHA3 ;
FINSI ;

CHAD = ('CHANGER' CHAM OBJMO CHPO0 'STRESSES');
CHAD = 'EXCO' ('MOTS' 'UX' 'UY' 'UZ') CHAD;

CHAM1 = CHAP 'ET' CHAD 'ET' CHAM0;
LCOM = 'EXTRAIRE' CHAM1 'COMP';

TABR = TABLE;
'REPETER' BO1 ('DIME' LCOM);
TABR.&BO1 = 'PROG';
'FIN' BO1;

***************************************************

MOZO = 'EXTRAIRE' OBJMO 'ZONE';

'OPTION' ERREUR 'CONTROLE';
J = 1;
'REPETER' BO0 (('DIME' MOZO)/2);
TMPMODEL = MOZO.J;
TMPMAILL = MOZO.(J '+' 1);
'REPETER' BO1 ('NBEL' TMPMAILL);
CHAM2 = 'REDU' CHAM1 ('ELEM' &BO1 TMPMAILL);
'REPETER' BO2 100;
TEST = 'EXTRAIRE' CHAM2 ('EXTRAIRE' LCOM 1) 1 1 &BO2;
'SI' (('EXISTE' TEST));
'REPETER' BO3 ('DIME' LCOM);
TMP1 = 'EXTRAIRE' CHAM2
 ('EXTRAIRE' LCOM &BO3) 1 1 &BO2;
TABR.&BO3 = (TABR.&BO3) 'ET' ('PROG' TMP1);
'FIN' BO3;
'SINON';
'QUITTER' BO2;
'FINSI';
'FIN' BO2;
'FIN' BO1;
J = J '+' 2;
'FIN' BO0;
'OPTION' ERREUR 'NORMALE';

'REPETER' BO1 ('DIME' (TABR.1));
TMP1 = 'EXTRAIRE' &BO1 (TABR.1);
TMP2 = 'EXTRAIRE' &BO1 (TABR.2);
SI (DIME3D EGA VRAI);
TMP3 = 'EXTRAIRE' &BO1 (TABR.3);
'SI' (&BO1 'EGA' 1);
PTGAUSS = TMP1 TMP2 TMP3;
'SINON';
PTGAUSS = PTGAUSS 'ET' (TMP1 TMP2 TMP3);
'FINSI';
SINON;
'SI' (&BO1 'EGA' 1);
PTGAUSS = TMP1 TMP2;
'SINON';
PTGAUSS = PTGAUSS 'ET' (TMP1 TMP2);
'FINSI';
FINSI;
'FIN' BO1;

SI (DIME3D EGA VRAI);
DEPGAUSS = 'MANUEL' 'CHPO' PTGAUSS
 'UX' (TABR.4) 'UY' (TABR.5) 'UZ' (TABR.6);
SINON;
DEPGAUSS = 'MANUEL' 'CHPO' PTGAUSS
 'UX' (TABR.3) 'UY' (TABR.4) 'UZ' (TABR.5);
FINSI;

TABV = TABLE;

SI (DIME3D EGA VRAI) ;

'REPETER' BO1 (('DIME' LCOM) '-' 6);
TMP1 = 'MANUEL' 'CHPO'  'NATU' 'DISCRET'
 PTGAUSS ('EXTRAIRE' (&BO1 '+' 6) LCOM) (TABR.(&BO1+6));
TABV.('EXTRAIRE' (&BO1 '+' 6) LCOM) = TMP1;
'FIN' BO1;
SINON ;
'REPETER' BO1 (('DIME' LCOM) '-' 5);
TMP1 = 'MANUEL' 'CHPO'  'NATU' 'DISCRET'
 PTGAUSS ('EXTRAIRE' (&BO1 '+' 5) LCOM) (TABR.(&BO1+5));
TABV.('EXTRAIRE' (&BO1 '+' 5) LCOM) = TMP1;
'FIN' BO1;
FINSI ;


'FINPROC' PTGAUSS DEPGAUSS TABV;

$$$$
