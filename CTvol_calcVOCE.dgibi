**************************************************************************
*              CALCUL ELASTOPLIQUE, LOI DE VOCE SANS FLUENCE             *
**************************************************************************
*
*----------------------------*
* NOM DE FICHIER A RESTITUER *
*----------------------------*
*
OPTI 'REST' 'CT_12_5_MAIL.sauv';
REST ;
*
OPTI TRAC 'PSC' ;
OPTI 'FTRA' 'CLMforce.ps' ;
*
*----------------------*
* PARAMETRE DE LA LOI  *
*----------------------*
*
ALPHA = 23.E-6 ;
PHI = 10. ;
T = 25. ;
* GTN
GTN = 1 ;
F00   = 0.0043 ;
FR0   = 0.056 ;
*
*------------*
* MATERIAUX  *
*------------*
*
SIG0 = 200. ;
YOUN1 = 70000. ;
RHO1 = 2700. ;
*
YOUN0 = 200000. ;
NU = 0.33 ;
RHO0 = 7850. ;
*
*-------------------------*
* CONDITIONS AUX LIMITES  *
*-------------------------*
*
DPLMAX = 1. ;
*
*----------------------* 
* INDICATEUR DE TRACE  *
*----------------------*
*
GRAPH = VRAI ;
*
*----------------------------------------------------------*
*                   MODELE ET MATERIAU                     *
*----------------------------------------------------------*
*
** MATERIAU GOUPILLE **
* 
MOD1 = 'MODE' GOUP 'MECANIQUE' 'ELASTIQUE' ; 
MAT1 = 'MATE' MOD1 'YOUN' YOUN0 'NU' NU 'RHO' RHO0 ;
*
** MATERIAU ENTA **
*
COEL = 'MOTS' 'YOUN' 'NU' 'RHO' 'PHI' ;

STATEV = 'MOTS' 'EEXX' 'EEYY' 'EEZZ' 'EEXY' 'EEXZ' 'EEYZ' 'P' ; 

MOD2 = 'MODELISER' ENTA 'MECANIQUE' 'ELASTIQUE' 'NON_LINEAIRE'
'UTILISATEUR' 'LIB_LOI' 'src/libUmatBehaviour.so' 'FCT_LOI' 'umatvoceirr'
'C_MATERIAU' COEL 'C_VARINTER' STATEV ;

MAT2 = 'MATERIAU' MOD2 'YOUN' YOUN1 'NU' NU 'RHO' RHO1 'PHI' PHI ;
*
** MATERIAU CT **
*
COEL = 'MOTS' 'YOUN' 'NU' 'RHO' 'PHI' ;

STATEV = 'MOTS' 'EEXX' 'EEYY' 'EEZZ' 'EEXY' 'EEXZ' 'EEYZ' 'P' ; 

MOD3 = 'MODELISER' CT 'MECANIQUE' 'ELASTIQUE' 'NON_LINEAIRE'
'UTILISATEUR' 'LIB_LOI' 'src/libUmatBehaviour.so' 'FCT_LOI' 'umatvoceirr'
'C_MATERIAU' COEL 'C_VARINTER' STATEV ;

MAT3 = 'MATERIAU' MOD3 'YOUN' YOUN1 'NU' NU 'RHO' RHO1 'PHI' PHI ;
*
*-----------------*
*  MATERIAU TOTAL *
*-----------------*
*
MODIN  = MOD2 ET MOD3 ;
MODTOT = MOD1 ET MODIN ;
MATTOT = MAT1 ET MAT2 ET MAT3 ;
*
*------------------------------------*
*       CONDITIONS AUX LIMITES       *
*------------------------------------* 
*
BLM1Z = BLOQ UZ_0 'UZ' ;
BLM2Y = BLOQ Lg0 'UY' ;
BLM3X = BLOQ CHARn 'UX' ;
BLM4Y = BLOQ CHARn 'UY' ;
*
CLM = BLM1Z ET BLM2Y ET BLM3X ET BLM4Y ;
*
DEP1 = DEPI BLM4Y DPLMAX ;

SI (GRAPH EGA VRAI) ;
    VEC1 = VECT (DEP1 NOMC 'UY') 5. 'UX' 'UY' COUL 'ROUGE' ;
    TRAC VEC1 (CTTOT) TITRE 'DEPLACEMENT IMPOSEE' ;
FINSI ; 
*
*------------------------------------*
*  CHARGEMENT EN DEPLACEMENT IMPOSEE *
*------------------------------------*
*
VAR0 = MANU CHML MODTOT 'EEXX' 0. 'EEYY' 0. 'EEZZ' 0. 'EEXY' 0. 'EEXZ' 0. 'EEYZ' 0. 'P' 0. ; 
*
TFIN = 1. ; 
EV0  = 'EVOL' 'MANU' TEMPS ('PROG' 0. TFIN) Y ('PROG' 0. 1. ) ; 
CHA0 = 'CHAR' 'DIMP' DEP1 EV0 ;
*
** IMPRESSION DES RESULTATS
LTPS = (PROG 0. PAS 1.E-2 1.) ;
** TEMPS DU CALCUL
LTPC = (PROG 0. PAS 1.E-2 1.) ;
*
*---------------------------------------------*
*      CREATION DE LA TABLE POUR PASAPAS      *
*---------------------------------------------*
*
TAB1                       = TABL ;
TAB1.'VARIABLES_INTERNES'  = TABLE ;
TAB1.VARIABLES_INTERNES.0  = VAR0 ;
TAB1.'MODELE'              = MODTOT ;
*
TAB1.'CARACTERISTIQUES'    = MATTOT ;
TAB1.'CHARGEMENT'          = CHA0 ;
TAB1.'BLOCAGES_MECANIQUES' = CLM ;
TAB1.'TEMPS_CALCULES'      = LTPC ;
TAB1.'TEMPS_SAUVES'        = LTPS ;
*
TAB1.'GRANDS_DEPLACEMENTS' = VRAI ;
TAB1.'LAGRANGIEN'          = 'MOT' 'REACTUALISE' ;
TAB1.'CONVERGENCE_FORCEE'  = VRAI ;
*
TAB1.'PROCEDURE_PERSO1'    = VRAI ;
TAB1.'IND'                 = 0 ;
TAB1.'DEP'                 = 0 ;
TAB1.'FOR'                 = 0 ;
*
PASAPAS TAB1 ;
LIST TAB1 ;
*
*-------------------- SAUVEGARDE DES DONNEES ------------------*
*
OPTI SAUV 'CONSERVER.sauv' ;

SAUV TAB0 TAB1 CHARn CMOD MODTOT MATTOT dim0
CHA0 CLM LTPS LTPC GOUP ENTA ENTA_CT CTTOT MOD1 MOD2 MAT1 
MAT2 SIG0 YOUN0 YOUN1 NU RHO0 RHO1 BLM4Y BLM1Z BLM2Y BLM3X
MAT3 MOD3 MODIN ;


OPTI DONN 5 ;
FIN ;
